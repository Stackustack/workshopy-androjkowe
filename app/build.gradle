apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'android-apt'
apply plugin: 'realm-android'

def AAVersion = '4.0.0'
def AndroidSupport = '25.0.1'
def DaggerVersion = '2.2'
def JavaxAnnotation = '1.0'
def FacebookStetho = '1.3.1'
def StethoRealm = '2.0.0'
def RecyclerViewEnhanced = '1.1.0'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "qaworkshops.android.netguru.co.qaworshopsandroid"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled true
            shrinkResources false
            useProguard false
            setProguardFiles([getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro',
                              'proguard-rules-test.pro'])
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            useProguard true
            setProguardFiles([getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'])
        }
    }

    productFlavors {
        mock {
            versionNameSuffix "-MOCK"
            applicationIdSuffix ".mock"
        }
        production {

        }
    }

    variantFilter { variant ->
        def buildName = variant.buildType.name
        def flavorName = variant.getFlavors().get(0).name
        // remove unnecessary variants
        ['release'].each {
            if (buildName.equals(it)) {
                ['mock'].each {
                    if (flavorName.equals(it)) {
                        variant.setIgnore(true);
                    }
                }
            }
        }
    }

    applicationVariants.all { variant ->
        // remove unaligned apk files
        variant.assemble.doLast {
            variant.outputs.each { output ->
                println "aligned " + output.outputFile
                println "unaligned " + output.packageApplication.outputFile

                File unaligned = output.packageApplication.outputFile;
                File aligned = output.outputFile
                if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                    println "deleting " + unaligned.getName()
                    unaligned.delete()
                }
            }
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile "com.android.support:appcompat-v7:$AndroidSupport"
    compile "com.android.support:cardview-v7:$AndroidSupport"
    compile "com.android.support:recyclerview-v7:$AndroidSupport"
    compile "com.android.support:support-v4:$AndroidSupport"
    compile "com.android.support:design:$AndroidSupport"

    apt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"

    apt "com.google.dagger:dagger-compiler:$DaggerVersion"
    compile "com.google.dagger:dagger:$DaggerVersion"
    provided "javax.annotation:jsr250-api:$JavaxAnnotation"

    compile "com.facebook.stetho:stetho:$FacebookStetho"
    compile "com.uphyca:stetho_realm:$StethoRealm"

    compile "com.nikhilpanju.recyclerviewenhanced:recyclerviewenhanced:$RecyclerViewEnhanced"

    testCompile 'junit:junit:4.12'
}
